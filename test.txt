PROGRAM -> OTHERFUNCTIONS MAINFUNCTION.
MAINFUNCTION -> tk_main STMTS tk_end.
OTHERFUNCTIONS -> FUNCTION OTHERFUNCTIONS | .
FUNCTION -> tk_funid INPUT_PAR OUTPUT_PAR tk_sem STMTS tk_end.
INPUT_PAR -> tk_input tk_parameter tk_list tk_sql PARAMETER_LIST tk_sqr.
OUTPUT_PAR -> tk_output tk_parameter tk_list tk_sql PARAMETER_LIST tk_sqr | .
PARAMETER_LIST -> DATATYPE tk_id REMAINING_LIST.
DATATYPE -> PRIMITIVEDATATYPE | CONSTRUCTEDDATATYPE.
PRIMITIVEDATATYPE -> tk_int | tk_real.
CONSTRUCTEDDATATYPE -> tk_record tk_ruid | tk_union tk_ruid | tk_ruid.
REMAINING_LIST -> tk_comma PARAMETER_LIST | .
STMTS -> TYPEDEFINITIONS DECLARATIONS OTHERSTMTS RETURNSTMT.
TYPEDEFINITIONS -> ACTUALORREDEFINED TYPEDEFINITIONS | .
ACTUALORREDEFINED -> TYPEDEFINITION | DEFINETYPESTMT.
TYPEDEFINITION -> tk_record tk_ruid FIELDDEFINITIONS tk_endrecord.
TYPEDEFINITION -> tk_union tk_ruid FIELDDEFINITIONS tk_endunion.
FIELDDEFINITIONS -> FIELDDEFINITION FIELDDEFINITION MOREFIELDS.
FIELDDEFINITION -> tk_type FIELDTYPE tk_colon tk_fieldid tk_sem.
FIELDTYPE -> PRIMITIVEDATATYPE | CONSTRUCTEDDATATYPE.
MOREFIELDS -> FIELDDEFINITION MOREFIELDS | .
DECLARATIONS -> DECLARATION DECLARATIONS | .
DECLARATION -> tk_type DATATYPE tk_colon tk_id GLOBAL_OR_NOT tk_sem.
GLOBAL_OR_NOT -> tk_colon tk_global | .
OTHERSTMTS -> STMT OTHERSTMTS | .
STMT -> ASSIGNMENTSTMT | ITERATIVESTMT | CONDITIONALSTMT | IOSTMT | FUNCALLSTMT.
ASSIGNMENTSTMT -> SINGLEORRECID tk_assignop ARITHMETICEXPRESSION tk_sem.
SINGLEORRECID -> tk_id OPTION_SINGLE_CONSTRUCTED.
OPTION_SINGLE_CONSTRUCTED ->  | ONEEXPANSION MOREEXPANSIONS.
ONEEXPANSION -> tk_dot tk_fieldid.
MOREEXPANSIONS -> ONEEXPANSION MOREEXPANSIONS | .
FUNCALLSTMT -> OUTPUTPARAMETERS tk_call tk_funid tk_with tk_parameters INPUTPARAMETERS tk_sem.
OUTPUTPARAMETERS -> tk_sql IDLIST tk_sqr tk_assignop | .
INPUTPARAMETERS -> tk_sql IDLIST tk_sqr.
ITERATIVESTMT -> tk_while tk_op BOOLEANEXPRESSION tk_cl STMT OTHERSTMTS tk_endwhile.
CONDITIONALSTMT -> tk_if tk_op BOOLEANEXPRESSION tk_cl tk_then STMT OTHERSTMTS ELSEPART.
ELSEPART -> tk_else STMT OTHERSTMTS tk_endif | tk_endif.
IOSTMT -> tk_read tk_op VAR tk_cl tk_sem | tk_write tk_op VAR tk_cl tk_sem.
ARITHMETICEXPRESSION -> TERM EXPPRIME.
EXPPRIME -> LOWPRECEDENCEOPERATORS TERM EXPPRIME | .
TERM -> FACTOR TERMPRIME.
TERMPRIME -> HIGHPRECEDENCEOPERATORS FACTOR TERMPRIME | .
FACTOR -> tk_op ARITHMETICEXPRESSION tk_cl | VAR.
HIGHPRECEDENCEOPERATORS -> tk_mul | tk_div.
LOWPRECEDENCEOPERATORS -> tk_plus | tk_minus.
BOOLEANEXPRESSION -> tk_op BOOLEANEXPRESSION tk_cl LOGICALOP tk_op BOOLEANEXPRESSION tk_cl.
BOOLEANEXPRESSION -> VAR RELATIONALOP VAR.
BOOLEANEXPRESSION -> tk_not BOOLEANEXPRESSION.
VAR -> SINGLEORRECID | tk_num | tk_rnum.
LOGICALOP -> tk_and | tk_or.
RELATIONALOP -> tk_lt | tk_le | tk_eq | tk_gt | tk_ge | tk_ne.
RETURNSTMT -> tk_return OPTIONALRETURN tk_sem.
OPTIONALRETURN -> tk_sql IDLIST tk_sqr | .
IDLIST -> tk_id MORE_IDS.
MORE_IDS -> tk_comma IDLIST | .
DEFINETYPESTMT -> tk_definetype A tk_ruid tk_as tk_ruid.
A -> tk_record | tk_union.